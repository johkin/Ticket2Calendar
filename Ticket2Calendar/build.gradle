buildscript {
	repositories {
		mavenCentral()
	}
	dependencies {
        classpath 'com.android.tools.build:gradle:0.13.+'
    }
}
apply plugin: 'com.android.application'

repositories {
    mavenCentral()
}

dependencies {
    compile files('libs/libGoogleAnalyticsServices_3.01.jar')
    compile 'com.android.support:support-v4:20.0.0'
}

task addSignProperties {
    def fileName ='signconfig.properties'
    def propFile = file(fileName)
    if (propFile.exists()) {
        def p = new Properties();
        p.load(new FileInputStream(propFile))
        p.propertyNames().each { name ->
            project.ext.set(name, p.getProperty(name))
        }
    } else {
        logger.quiet "To sign the application, add a properties-file '$fileName' with the " +
                "keys storePassword and keyPassword."
    }
}

android {
    compileSdkVersion 20
    buildToolsVersion '20.0.0'

    signingConfigs {
        release {
            storeFile file("../market-publisher-key")
            keyAlias "acrend"
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix = ".debug"
            buildConfigField "boolean", "STAGING", "false"
        }
        release {
            buildConfigField "boolean", "STAGING", "false"
            signingConfig signingConfigs.release
        }
    }

    defaultConfig {
        versionCode = 137
        versionName = '1.3.7'
    }

    if (project.hasProperty('storePassword') &&
            project.hasProperty('keyPassword')) {
        android.signingConfigs.release.storePassword = storePassword
        android.signingConfigs.release.keyPassword = keyPassword
    } else {
        buildTypes.release.signingConfig = null
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}
